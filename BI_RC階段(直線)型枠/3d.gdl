hotspot 0,0,0
hotspot 0,stairWid,0


!====================================================================================階段
group "stair_part1"
    !材質
    top_material=stairMat
    bottom_material=stairMat
    mat1=stairMat
    mat2=stairMat
    mat3=stairMat

    !躯体
    for i=1 to stairNum
        ruledsegmented{2} top_material,bottom_material,3,1+2+4+8+16+32,0,
        0           ,stairStart,0                  ,0,mat1,
        stairTread  ,stairStart,stairRiser         ,0,mat2,
        -stairNosing,stairStart,stairRiser-stairRat,0,mat3,
        0           ,stairEnd  ,0                  ,
        stairTread  ,stairEnd  ,stairRiser         ,
        -stairNosing,stairEnd  ,stairRiser-stairRat
    add stairTread,0,stairRiser
    next i
    del stairNum
endgroup


group "stair_part2"
    !材質
    top_material=stairMat
    bottom_material=stairMat
    mat1=stairMat
    mat2=stairMat
    mat3=stairMat
    mat4=stairMat

    !躯体
    if switchLanding then
        ruledsegmented{2} top_material,bottom_material,4,1+2+4+8+16+32,0,
        0                                           ,stairStart,0                             ,0,mat1,
        stairNum*stairTread                         ,stairStart,stairNum*stairRiser           ,0,mat2,
        (stairNum*stairRiser-landingThi-equB1)/equK1,stairStart,stairNum*stairRiser-landingThi,0,mat3,
        -equB1/equK1                                ,stairStart,0                             ,0,mat4,
        0                                           ,stairEnd  ,0                             ,
        stairNum*stairTread                         ,stairEnd  ,stairNum*stairRiser           ,
        (stairNum*stairRiser-landingThi-equB1)/equK1,stairEnd  ,stairNum*stairRiser-landingThi,
        -equB1/equK1                                ,stairEnd  ,0
    else
        ruledsegmented{2} top_material,bottom_material,4,1+2+4+8+16+32,0,
        0                  ,stairStart,0                                ,0,mat1,
        stairNum*stairTread,stairStart,stairNum*stairRiser              ,0,mat2,
        stairNum*stairTread,stairStart,equK1*(stairNum*stairTread)+equB1,0,mat3,
        -equB1/equK1       ,stairStart,0                                ,0,mat1,
        0                  ,stairEnd  ,0                                ,
        stairNum*stairTread,stairEnd  ,stairNum*stairRiser              ,
        stairNum*stairTread,stairEnd  ,equK1*(stairNum*stairTread)+equB1,
        -equB1/equK1       ,stairEnd  ,0
    endif
endgroup


!====================================================================================踊り場
group "landing"
    !材質
    top_material=stairMat
    bottom_material=stairMat
    mat1=stairMat
    mat2=stairMat
    mat3=stairMat
    mat4=stairMat

    !躯体
    if switchLanding then
        ruledsegmented{2} top_material,bottom_material,4,1+2+4+8+16+32,0,
        stairNum*stairTread                         ,stairStart,stairNum*stairRiser                          ,0,mat1,
        stairNum*stairTread+landingDep              ,stairStart,stairNum*stairRiser-landingDep*landingRat/100,0,mat2,
        stairNum*stairTread+landingDep              ,stairStart,stairNum*stairRiser-landingThi               ,0,mat3,
        (stairNum*stairRiser-landingThi-equB1)/equK1,stairStart,stairNum*stairRiser-landingThi               ,0,mat4,
        stairNum*stairTread                         ,stairEnd  ,stairNum*stairRiser                          ,
        stairNum*stairTread+landingDep              ,stairEnd  ,stairNum*stairRiser-landingDep*landingRat/100,
        stairNum*stairTread+landingDep              ,stairEnd  ,stairNum*stairRiser-landingThi               ,
        (stairNum*stairRiser-landingThi-equB1)/equK1,stairEnd  ,stairNum*stairRiser-landingThi               
    endif
endgroup

stair_body1 = addgroup ("stair_part1","stair_part2")
stair_body2 = addgroup (stair_body1,"landing")
placegroup stair_body2


!====================================================================================立上り
!右側立上り
group "handrail1"
    !材質
    top_material=stairMat
    bottom_material=stairMat
    mat1=stairMat
    mat2=stairMat
    mat3=stairMat
    mat4=stairMat
    mat5=stairMat
    mat6=stairMat
    mat7=stairMat

    !躯体
    if switchRail1 then
        if switchLanding then
            ruledsegmented{2} top_material,bottom_material,7,1+2+4+8+16+32,0,
            -stairTread                                     ,0       ,stairRailHei                      ,0,mat1,
            -stairTread                                     ,0       ,0                                 ,0,mat2,
            -equB1/equK1                                    ,0       ,0                                 ,0,mat3,
            (stairNum*stairRiser-landingThi-equB1)/equK1    ,0       ,stairNum*stairRiser-landingThi    ,0,mat4,
            stairNum*stairTread+landingDep                  ,0       ,stairNum*stairRiser-landingThi    ,0,mat5,
            stairNum*stairTread+landingDep                  ,0       ,stairNum*stairRiser+landingRailHei,0,mat6,
            (stairNum*stairRiser+landingRailHei-equB2)/equK2,0       ,stairNum*stairRiser+landingRailHei,0,mat7,
            -stairTread                                     ,rail1Wid,stairRailHei                      ,
            -stairTread                                     ,rail1Wid,0                                 ,
            -equB1/equK1                                    ,rail1Wid,0                                 ,
            (stairNum*stairRiser-landingThi-equB1)/equK1    ,rail1Wid,stairNum*stairRiser-landingThi    ,
            stairNum*stairTread+landingDep                  ,rail1Wid,stairNum*stairRiser-landingThi    ,
            stairNum*stairTread+landingDep                  ,rail1Wid,stairNum*stairRiser+landingRailHei,
            (stairNum*stairRiser+landingRailHei-equB2)/equK2,rail1Wid,stairNum*stairRiser+landingRailHei
        else
            ruledsegmented{2} top_material,bottom_material,6,1+2+4+8+16+32,0,
            -stairTread                                   ,0       ,stairRailHei                     ,0,mat1,
            -stairTread                                   ,0       ,0                                ,0,mat2,
            -equB1/equK1                                  ,0       ,0                                ,0,mat3,
            stairNum*stairTread                           ,0       ,equK1*(stairNum*stairTread)+equB1,0,mat5,
            stairNum*stairTread                           ,0       ,stairNum*stairRiser+stairRailHei ,0,mat6,
            (stairNum*stairRiser+stairRailHei-equB2)/equK2,0       ,stairNum*stairRiser+stairRailHei ,0,mat7,
            -stairTread                                   ,rail1Wid,stairRailHei                     ,
            -stairTread                                   ,rail1Wid,0                                ,
            -equB1/equK1                                  ,rail1Wid,0                                ,
            stairNum*stairTread                           ,rail1Wid,equK1*(stairNum*stairTread)+equB1,
            stairNum*stairTread                           ,rail1Wid,stairNum*stairRiser+stairRailHei ,
            (stairNum*stairRiser+stairRailHei-equB2)/equK2,rail1Wid,stairNum*stairRiser+stairRailHei 
        endif
    endif
endgroup

if switchRail1 then
    placegroup "handrail1"
endif


!左側立上り
group "handrail2"
    !材質
    top_material=stairMat
    bottom_material=stairMat
    mat1=stairMat
    mat2=stairMat
    mat3=stairMat
    mat4=stairMat
    mat5=stairMat
    mat6=stairMat
    mat7=stairMat

    !躯体
    if switchRail2 then
        if switchLanding then
            ruledsegmented{2} top_material,bottom_material,7,1+2+4+8+16+32,0,
            -stairTread                                     ,stairWid         ,stairRailHei                      ,0,mat1,
            -stairTread                                     ,stairWid         ,0                                 ,0,mat2,
            -equB1/equK1                                    ,stairWid         ,0                                 ,0,mat3,
            (stairNum*stairRiser-landingThi-equB1)/equK1    ,stairWid         ,stairNum*stairRiser-landingThi    ,0,mat4,
            stairNum*stairTread+landingDep                  ,stairWid         ,stairNum*stairRiser-landingThi    ,0,mat5,
            stairNum*stairTread+landingDep                  ,stairWid         ,stairNum*stairRiser+landingRailHei,0,mat6,
            (stairNum*stairRiser+landingRailHei-equB2)/equK2,stairWid         ,stairNum*stairRiser+landingRailHei,0,mat7,
            -stairTread                                     ,stairWid-rail2Wid,stairRailHei                      ,
            -stairTread                                     ,stairWid-rail2Wid,0                                 ,
            -equB1/equK1                                    ,stairWid-rail2Wid,0                                 ,
            (stairNum*stairRiser-landingThi-equB1)/equK1    ,stairWid-rail2Wid,stairNum*stairRiser-landingThi    ,
            stairNum*stairTread+landingDep                  ,stairWid-rail2Wid,stairNum*stairRiser-landingThi    ,
            stairNum*stairTread+landingDep                  ,stairWid-rail2Wid,stairNum*stairRiser+landingRailHei,
            (stairNum*stairRiser+landingRailHei-equB2)/equK2,stairWid-rail2Wid,stairNum*stairRiser+landingRailHei
        else
            ruledsegmented{2} top_material,bottom_material,6,1+2+4+8+16+32,0,
            -stairTread                                   ,stairWid         ,stairRailHei                     ,0,mat1,
            -stairTread                                   ,stairWid         ,0                                ,0,mat2,
            -equB1/equK1                                  ,stairWid         ,0                                ,0,mat3,
            stairNum*stairTread                           ,stairWid         ,equK1*(stairNum*stairTread)+equB1,0,mat5,
            stairNum*stairTread                           ,stairWid         ,stairNum*stairRiser+stairRailHei ,0,mat6,
            (stairNum*stairRiser+stairRailHei-equB2)/equK2,stairWid         ,stairNum*stairRiser+stairRailHei ,0,mat7,
            -stairTread                                   ,stairWid-rail2Wid,stairRailHei                     ,
            -stairTread                                   ,stairWid-rail2Wid,0                                ,
            -equB1/equK1                                  ,stairWid-rail2Wid,0                                ,
            stairNum*stairTread                           ,stairWid-rail2Wid,equK1*(stairNum*stairTread)+equB1,
            stairNum*stairTread                           ,stairWid-rail2Wid,stairNum*stairRiser+stairRailHei ,
            (stairNum*stairRiser+stairRailHei-equB2)/equK2,stairWid-rail2Wid,stairNum*stairRiser+stairRailHei 
        endif
    endif
endgroup

if switchRail2 then
    placegroup "handrail2"
endif


!====================================================================================型枠
!階段型枠
group "stepFrame"
    material frameMat

    for i=1 to stairNum
        ruledsegmented 4,1+2+4+8+16+32,
        0                    ,stairStart,0                  ,0,
        -stairNosing         ,stairStart,stairRiser-stairRat,0,
        -stairNosing-frameThi,stairStart,stairRiser-stairRat,0,
        -frameThi            ,stairStart,0                  ,0,
        0                    ,stairEnd  ,0                  ,
        -stairNosing         ,stairEnd  ,stairRiser-stairRat,
        -stairNosing-frameThi,stairEnd  ,stairRiser-stairRat,
        -frameThi            ,stairEnd  ,0                  
    add stairTread,0,stairRiser
    next i
    del stairNum

    for i=1 to stairNum-1
        ruledsegmented 4,1+2+4+8+16+32,
        -stairNosing-frameThi,stairStart,stairRiser-stairRat         ,0,
        stairTread-frameThi  ,stairStart,stairRiser                  ,0,
        stairTread-frameThi  ,stairStart,stairRiser+frameThi         ,0,
        -stairNosing-frameThi,stairStart,stairRiser-stairRat+frameThi,0,
        -stairNosing-frameThi,stairEnd  ,stairRiser-stairRat         ,
        stairTread-frameThi  ,stairEnd  ,stairRiser                  ,
        stairTread-frameThi  ,stairEnd  ,stairRiser+frameThi         ,
        -stairNosing-frameThi,stairEnd  ,stairRiser-stairRat+frameThi
    add stairTread,0,stairRiser
    next i
    del stairNum-1
endgroup

if disFrame and disStepFrame then
    placegroup "stepFrame"
endif


!立上り型枠
group "handrailFrame"
    material frameMat

    if switchLanding then
        topX=(stairNum*stairRiser+landingRailHei-equB2)/equK2
        topZ=stairNum*stairRiser+landingRailHei
    else
        topX=(stairNum*stairRiser+stairRailHei-equB2)/equK2
        topZ=stairNum*stairRiser+stairRailHei
    endif

    if switchRail1 then
        !top
        ruledsegmented 4,1+2+4+8+16+32,
        -stairTread         ,-frameThi        ,0           ,0,
        -stairTread         ,-frameThi        ,stairRailHei,0,
        -stairTread-frameThi,-frameThi        ,stairRailHei,0,
        -stairTread-frameThi,-frameThi        ,0           ,0,
        -stairTread         ,rail1Wid+frameThi,0           ,
        -stairTread         ,rail1Wid+frameThi,stairRailHei,
        -stairTread-frameThi,rail1Wid+frameThi,stairRailHei,
        -stairTread-frameThi,rail1Wid+frameThi,0

        ruledsegmented 4,1+2+4+8+16+32,
        -stairTread         ,-frameThi        ,stairRailHei ,0,
        topX                ,-frameThi        ,topZ         ,0,
        topX                ,-frameThi        ,topZ+frameThi,0,
        -stairTread-frameThi,-frameThi        ,stairRailHei ,0,
        -stairTread         ,rail1Wid+frameThi,stairRailHei ,
        topX                ,rail1Wid+frameThi,topZ         ,
        topX                ,rail1Wid+frameThi,topZ+frameThi,
        -stairTread-frameThi,rail1Wid+frameThi,stairRailHei

        !outside
        for i=1 to handrailOutNum
            put _handrailOutPos[i][1],0,_handrailOutPos[i][2],0
        next i

        for i=1 to handrailOutNum
            put _handrailOutPos[i][1],-frameThi,_handrailOutPos[i][2]
        next i

        ruledsegmented handrailOutNum,1+2+4+8+16+32,
        get(nsp)

        !inside
        for i=1 to handrailInNum
            put _handrailInPos[i][1],rail1Wid,_handrailInPos[i][2],0
        next i

        for i=1 to handrailInNum
            put _handrailInPos[i][1],rail1Wid+frameThi,_handrailInPos[i][2]
        next i

        ruledsegmented handrailInNum,1+2+4+8+16+32,
        get(nsp)
    endif

    if switchRail2 then
        !top
        ruledsegmented 4,1+2+4+8+16+32,
        -stairTread         ,stairWid+frameThi         ,0           ,0,
        -stairTread         ,stairWid+frameThi         ,stairRailHei,0,
        -stairTread-frameThi,stairWid+frameThi         ,stairRailHei,0,
        -stairTread-frameThi,stairWid+frameThi         ,0           ,0,
        -stairTread         ,stairWid-rail2Wid-frameThi,0           ,
        -stairTread         ,stairWid-rail2Wid-frameThi,stairRailHei,
        -stairTread-frameThi,stairWid-rail2Wid-frameThi,stairRailHei,
        -stairTread-frameThi,stairWid-rail2Wid-frameThi,0

        ruledsegmented 4,1+2+4+8+16+32,
        -stairTread         ,stairWid+frameThi         ,stairRailHei ,0,
        topX                ,stairWid+frameThi         ,topZ         ,0,
        topX                ,stairWid+frameThi         ,topZ+frameThi,0,
        -stairTread-frameThi,stairWid+frameThi         ,stairRailHei ,0,
        -stairTread         ,stairWid-rail2Wid-frameThi,stairRailHei ,
        topX                ,stairWid-rail2Wid-frameThi,topZ         ,
        topX                ,stairWid-rail2Wid-frameThi,topZ+frameThi,
        -stairTread-frameThi,stairWid-rail2Wid-frameThi,stairRailHei

        !outside
        for i=1 to handrailOutNum
            put _handrailOutPos[i][1],stairWid,_handrailOutPos[i][2],0
        next i

        for i=1 to handrailOutNum
            put _handrailOutPos[i][1],stairWid+frameThi,_handrailOutPos[i][2]
        next i

        ruledsegmented handrailOutNum,1+2+4+8+16+32,
        get(nsp)

        !inside
        for i=1 to handrailInNum
            put _handrailInPos[i][1],stairWid-rail2Wid,_handrailInPos[i][2],0
        next i

        for i=1 to handrailInNum
            put _handrailInPos[i][1],stairWid-rail2Wid-frameThi,_handrailInPos[i][2]
        next i

        ruledsegmented handrailInNum,1+2+4+8+16+32,
        get(nsp)
    endif
endgroup

if disHandrailFrame then
    placegroup "handrailFrame"
endif


!階段裏面型枠
group "stepBackFrame"
    material frameMat

    if switchLanding then
        ruledsegmented 4,1+2+4+8+16+32,
        -equB1/equK1                                ,-frameThi*switchRail1        ,0                                      ,0,
        (stairNum*stairRiser-landingThi-equB1)/equK1,-frameThi*switchRail1        ,stairNum*stairRiser-landingThi         ,0,
        (stairNum*stairRiser-landingThi-equB1)/equK1,-frameThi*switchRail1        ,stairNum*stairRiser-landingThi-frameThi,0,
        -equB1/equK1+frameThi/sin(stairAngle)       ,-frameThi*switchRail1        ,0                                      ,0,
        -equB1/equK1                                ,stairWid+frameThi*switchRail2,0                                      ,
        (stairNum*stairRiser-landingThi-equB1)/equK1,stairWid+frameThi*switchRail2,stairNum*stairRiser-landingThi         ,
        (stairNum*stairRiser-landingThi-equB1)/equK1,stairWid+frameThi*switchRail2,stairNum*stairRiser-landingThi-frameThi,
        -equB1/equK1+frameThi/sin(stairAngle)       ,stairWid+frameThi*switchRail2,0
    else
        ruledsegmented 4,1+2+4+8+16+32,
        -equB1/equK1                         ,-frameThi*switchRail1        ,0                                         ,0,
        stairNum*stairTread                  ,-frameThi*switchRail1        ,equK1*(stairNum*stairTread)+equB1         ,0,
        stairNum*stairTread                  ,-frameThi*switchRail1        ,equK1*(stairNum*stairTread)+equB1-frameThi,0,
        -equB1/equK1+frameThi/sin(stairAngle),-frameThi*switchRail1        ,0                                         ,0,
        -equB1/equK1                         ,stairWid+frameThi*switchRail2,0                                         ,
        stairNum*stairTread                  ,stairWid+frameThi*switchRail2,equK1*(stairNum*stairTread)+equB1         ,
        stairNum*stairTread                  ,stairWid+frameThi*switchRail2,equK1*(stairNum*stairTread)+equB1-frameThi,
        -equB1/equK1+frameThi/sin(stairAngle),stairWid+frameThi*switchRail2,0                                         
    endif
endgroup

if disFrame and disStepBackFrame then
    placegroup "stepBackFrame"
endif


!踊り場裏面型枠
group "landingBackFrame"
    material frameMat

    ruledsegmented 4,1+2+4+8+16+32,
    stairNum*stairTread+landingDep              ,-frameThi*switchRail1        ,stairNum*stairRiser-landingThi         ,0,
    (stairNum*stairRiser-landingThi-equB1)/equK1,-frameThi*switchRail1        ,stairNum*stairRiser-landingThi         ,0,
    (stairNum*stairRiser-landingThi-equB1)/equK1,-frameThi*switchRail1        ,stairNum*stairRiser-landingThi-frameThi,0,
    stairNum*stairTread+landingDep              ,-frameThi*switchRail1        ,stairNum*stairRiser-landingThi-frameThi,0,
    stairNum*stairTread+landingDep              ,stairWid+frameThi*switchRail2,stairNum*stairRiser-landingThi         ,
    (stairNum*stairRiser-landingThi-equB1)/equK1,stairWid+frameThi*switchRail2,stairNum*stairRiser-landingThi         ,
    (stairNum*stairRiser-landingThi-equB1)/equK1,stairWid+frameThi*switchRail2,stairNum*stairRiser-landingThi-frameThi,
    stairNum*stairTread+landingDep              ,stairWid+frameThi*switchRail2,stairNum*stairRiser-landingThi-frameThi
endgroup  

if switchLanding and disFrame and disLandingBackFrame then
    placegroup "landingBackFrame"
endif


