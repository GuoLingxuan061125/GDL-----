if dis_floor_2D=0 then end

font_size=0.7

define style "moji1" textfont,font_size*100/glob_scale,5,0
style "moji1"

!上下左右反転
if turn_left_right then mul2 -1,1
if turn_up_down then mul2 1,-1


line_type line_type_all

hotspot2 0,0

!==================================階段
step_gap=step_tread/2

!直線階段
if stair_mode=1 then
	!UP
	if dis_floor_2D<floor_num then
		temp_x1=0
		temp_x2=(step_num_tot-1)*step_tread/2-step_gap/2
		switch_mark=1
		temp_text="UP"
		temp_direction=1

		gosub "階段sub"
	endif

	!DOWN
	if dis_floor_2D>1 then
		if switch_floor then
			temp_x1=(step_num_tot-1)*step_tread
		else
			temp_x1=step_num_tot*step_tread
		endif
		temp_x2=(step_num_tot-1)*step_tread/2+step_gap/2
		switch_mark=1
		temp_text="DN"
		temp_direction=-1

		gosub "階段sub"

		if switch_floor then
			rect2 floor_x,0,floor_x-step_thi,step_wid1
		endif
	endif
endif

!廻り階段
if stair_mode=2 then
	!下部階段
	if dis_floor_2D>1 then
		temp_x1=(step_num_down-1)*step_tread
		temp_x2=(step_num_down-1)*step_tread/2+step_gap/2
		switch_mark=0
		temp_direction=-1

		gosub "階段sub"

		line2 (temp_x1+temp_x2)/2,step_wid1/2,landing_x,step_wid1/2
		rect2 landing_x,0,landing_x-step_thi,step_wid1
	endif

	!上部階段
	if dis_floor_2D>1 then
		if switch_floor then
			temp_x1=floor_x+step_thi
		else
			temp_x1=(step_num_down-step_num_up-1)*step_tread+landing_size1-landing_size2+step_thi
		endif
		temp_x2=landing_x+landing_size1-landing_size2
		temp_direction=-1

		add2 0,step_wid1+landing_gap
			gosub "上部階段sub"
			
			if switch_floor then
				rect2 floor_x,0,floor_x+step_thi,step_wid1
			endif
		del 1
	endif

	!下部階段
	if dis_floor_2D<floor_num then
		temp_x1=0
		temp_x2=(step_num_down-1)*step_tread/2-step_gap/2
		if dis_floor_2D=1 then
			switch_mark=1
			temp_text="UP"
		else
			switch_mark=0
		endif
		temp_direction=1

		gosub "階段sub"
	endif
endif

!直線+廻り階段
if stair_mode=3 then
	!直線階段
	if dis_floor_2D=1 then
		temp_x1=0
		temp_x2=(step_num_tot-1)*step_tread/2-step_gap/2
		switch_mark=1
		temp_text="UP"
		temp_direction=1

		gosub "階段sub"
	endif

	if dis_floor_2D=2 then
		if switch_floor then
			temp_x1=(step_num_tot-1)*step_tread
		else
			temp_x1=step_num_tot*step_tread
		endif
		temp_x2=(step_num_tot-1)*step_tread/2+step_gap/2
		switch_mark=1
		temp_text="DN"
		temp_direction=-1

		gosub "階段sub"

		if switch_floor then
			rect2 floor_x,0,floor_x-step_thi,step_wid1
		endif
	endif

	!下部階段
	if dis_floor_2D>1 and dis_floor_2D<floor_num then
		temp_x1=floor_x+floor_size1-floor_size2
		temp_x2=floor_x+floor_size1-floor_size2-(step_num_up-1)*step_tread/2+step_gap/2
		if dis_floor_2D=2 then
			switch_mark=1
			temp_text="UP"
		else
			switch_mark=0
		endif
		temp_direction=-1

		add2 0,step_wid1+landing_gap
			gosub "階段sub"
		del 1
	endif

	!上部階段
	if dis_floor_2D>2 then
		if switch_floor then
			temp_x1=floor_x-step_thi
		else
			temp_x1=floor_x-step_thi+step_tread
		endif
		temp_x2=landing_x-landing_size1+landing_size2
		temp_direction=1

		gosub "上部階段sub"

		if switch_floor then
			rect2 floor_x,0,floor_x-step_thi,step_wid1
		endif
	endif

	!下部階段
	if dis_floor_2D>2 then
		temp_x1=landing_x+step_thi
		temp_x2=landing_x+step_thi+(step_num_down-1)*step_tread/2-step_gap/2
		switch_mark=0
		temp_direction=1

		add2 0,step_wid1+landing_gap
			gosub "階段sub"

			line2 (temp_x1+temp_x2)/2,step_wid2/2,landing_x,step_wid2/2
			rect2 landing_x,0,landing_x+step_thi,step_wid2
		del 1
	endif
endif


!==================================中間踊り場
if switch_landing then
	!廻り階段
	if stair_mode=2 then
		if dis_floor_2D>1 then
			add2 landing_x,0
				gosub "踊場sub"
			del 1
		endif
	endif

	!直線+廻り階段
	if stair_mode=3 then
		if dis_floor_2D>2 then
			add2 landing_x,step_wid_tot
			rot2 180
				gosub "踊場sub"
			del 2
		endif
	endif
endif


!==================================フロア踊り場
if switch_floor then
	!直線階段
	if stair_mode=1 then
		for i=1 to 2
			if (i=1 and dis_floor_2D>1) or (i=2 and dis_floor_2D>1 and dis_floor_2D<floor_num) then
				if i=1 then add2 floor_x,0
				if i=2 then mul2 -1,1
					!外枠
					rect2 0,0,floor_len,step_wid1

					!hotspot
					hotspot2 0,0
					hotspot2 0,step_wid1
					hotspot2 floor_len,0
					hotspot2 floor_len,step_wid1

					hotspot2 0        ,step_wid1/2,code,floor_len,1+128 : code=code+1
					hotspot2 floor_len,step_wid1/2,code,floor_len,2     : code=code+1
					hotspot2 -1       ,step_wid1/2,code,floor_len,3     : code=code+1

					!文字
					add2 floor_len/2,step_wid1/2
					if turn_left_right then mul2 -1,1
					if turn_up_down then mul2 1,-1
						text2 0,0,str(dis_floor_2D,1,0)+"階廊下"
					if turn_left_right then del 1
					if turn_up_down then del 1
					del 1
				del 1
			endif
		next i
	endif

	!廻り階段
	if stair_mode=2 then
		if dis_floor_2D>1 then
			add2 floor_x,step_wid_tot
			rot2 180
				gosub "廊下sub"
			del 2
		endif
	endif

	!直線+廻り階段
	if stair_mode=3 then
		if dis_floor_2D>1 then
			add2 floor_x,0
				gosub "廊下sub"
			del 1
		endif
	endif
endif



if turn_left_right then del 1
if turn_up_down then del 1



end


"階段sub":
	!外枠
	line2 temp_x1,0,temp_x1,step_wid1
	line2 temp_x1,0,temp_x2,0
	line2 temp_x1,step_wid1,temp_x2,step_wid1
	poly2 6,1,
		temp_x2,0,
		temp_x2,step_wid1/2-step_gap/2,
		temp_x2-step_gap/2,step_wid1/2-step_gap/4,
		temp_x2+step_gap/2,step_wid1/2+step_gap/4,
		temp_x2,step_wid1/2+step_gap/2,
		temp_x2,step_wid1

	!hotspot
	hotspot2 temp_x1,0
	hotspot2 temp_x1,step_wid1
	hotspot2 temp_x2,0
	hotspot2 temp_x2,step_wid1

	!階段
	add2 temp_x1,0
		for i=1 to ceil(abs(temp_x2-temp_x1)/step_tread)
			line2 0,0,0,step_wid1
		add2 step_tread*temp_direction,0
		next i
		del ceil(abs(temp_x2-temp_x1)/step_tread)
	del 1

	if switch_mark=1 then
		!矢印
		arrow_start=temp_x1+0.2*(temp_x2-temp_x1)
		arrow_end=temp_x1+0.8*(temp_x2-temp_x1)

		line2 arrow_start,step_wid1/2,arrow_end,step_wid1/2
		line2 arrow_end,step_wid1/2,arrow_end-0.1*temp_direction,step_wid1/2+0.03
		line2 arrow_end,step_wid1/2,arrow_end-0.1*temp_direction,step_wid1/2-0.03

		!文字
		add2 (arrow_start+arrow_end)/2,step_wid1/2
		if turn_left_right then mul2 -1,1
		if turn_up_down then mul2 1,-1
			add2 0,font_size/10
				text2 0,0,temp_text
			del 1
		if turn_left_right then del 1
		if turn_up_down then del 1
		del 1
	endif
return


"上部階段sub":
	!外枠
	rect2 temp_x1,0,temp_x2,step_wid1

	!hotspot
	hotspot2 temp_x1,0
	hotspot2 temp_x2,0
	hotspot2 temp_x1,step_wid1
	hotspot2 temp_x2,step_wid1

	!階段
	add2 temp_x1,0
		for i=1 to ceil(abs(temp_x2-temp_x1)/step_tread)
			line2 0,0,0,step_wid1
		add2 -step_tread*temp_direction,0
		next i
		del ceil(abs(temp_x2-temp_x1)/step_tread)
	del 1

	!矢印
	line2 temp_x2,step_wid1-step_wid2/2,(temp_x1+temp_x2)/2,step_wid1-step_wid2/2
	line2 (temp_x1+temp_x2)/2,step_wid1-step_wid2/2,(temp_x1+temp_x2)/2-0.1*temp_direction,step_wid1-step_wid2/2+0.03
	line2 (temp_x1+temp_x2)/2,step_wid1-step_wid2/2,(temp_x1+temp_x2)/2-0.1*temp_direction,step_wid1-step_wid2/2-0.03

	!文字
	add2 (temp_x1+temp_x2)/2,step_wid1-step_wid2/2
	if turn_left_right then mul2 -1,1
	if turn_up_down then mul2 1,-1
		add2 font_size/10*temp_direction,0
			text2 0,0,"UP"
		del 1
	if turn_left_right then del 1
	if turn_up_down then del 1
	del 1
return


"踊場sub":
	!外枠
	poly2 9,1,
		0,0,
		landing_size1+landing_len,0,
		landing_size1+landing_len,step_wid_tot,
		landing_size1-landing_size2,step_wid_tot,
		landing_size1-landing_size2,step_wid1+landing_gap,
		landing_size1,step_wid1+landing_gap,
		landing_size1,step_wid1,
		0,step_wid1,
		0,0

	!hotspot
	hotspot2 0,0
	hotspot2 landing_size1+landing_len,0
	hotspot2 landing_size1+landing_len,step_wid_tot
	hotspot2 landing_size1-landing_size2,step_wid_tot
	hotspot2 landing_size1-landing_size2,step_wid1+landing_gap
	hotspot2 landing_size1,step_wid1+landing_gap
	hotspot2 landing_size1,step_wid1
	hotspot2 0,step_wid1

	!調整hotspot
	hotspot2 0            ,step_wid1/2,code,landing_size1,1+128 : code=code+1
	hotspot2 landing_size1,step_wid1/2,code,landing_size1,2     : code=code+1
	hotspot2 -1           ,step_wid1/2,code,landing_size1,3     : code=code+1

	hotspot2 landing_size1            ,step_wid_tot/2,code,landing_len,1+128 : code=code+1
	hotspot2 landing_size1+landing_len,step_wid_tot/2,code,landing_len,2     : code=code+1
	hotspot2 landing_size1-1          ,step_wid_tot/2,code,landing_len,3     : code=code+1

	if stair_mode=2 then
		hotspot2 landing_size1              ,step_wid_tot-step_wid2/2,code,landing_size2,1+128 : code=code+1
		hotspot2 landing_size1-landing_size2,step_wid_tot-step_wid2/2,code,landing_size2,2     : code=code+1
		hotspot2 landing_size1+1            ,step_wid_tot-step_wid2/2,code,landing_size2,3     : code=code+1
	endif

	!矢印
	poly2 4,1,
		0,step_wid1/2,
		landing_size1+landing_len/2,step_wid1/2,
		landing_size1+landing_len/2,step_wid_tot-step_wid2/2,
		landing_size1-landing_size2,step_wid_tot-step_wid2/2

	!文字
	add2 landing_size1+landing_len/2,step_wid_tot/2
		if turn_left_right then mul2 -1,1
		if turn_up_down then mul2 1,-1
			rot2 -90
			add2 0,font_size/10
				text2 0,0,"中"+str(dis_floor_2D,1,0)+"階踊場"
			del 2
		if turn_left_right then del 1
		if turn_up_down then del 1
	del 1
return


"廊下sub":
	if dis_floor_2D=floor_num then
		!外枠
		poly2 7,1,
			0,0,
			floor_size1+floor_len,0,
			floor_size1+floor_len,step_wid_tot,
			floor_size1,step_wid_tot,
			floor_size1,step_wid1,
			0,step_wid1,
			0,0

		!hotspot
		hotspot2 0,0
		hotspot2 floor_size1+floor_len,0
		hotspot2 floor_size1+floor_len,step_wid_tot
		hotspot2 floor_size1,step_wid_tot
		hotspot2 floor_size1,step_wid1
		hotspot2 0,step_wid1
	else
		!外枠
		poly2 9,1,
			0,0,
			floor_size1+floor_len,0,
			floor_size1+floor_len,step_wid_tot,
			floor_size1-floor_size2,step_wid_tot,
			floor_size1-floor_size2,step_wid1+landing_gap,
			floor_size1,step_wid1+landing_gap,
			floor_size1,step_wid1,
			0,step_wid1,
			0,0

		!hotspot
		hotspot2 0,0
		hotspot2 floor_size1+floor_len,0
		hotspot2 floor_size1+floor_len,step_wid_tot
		hotspot2 floor_size1-floor_size2,step_wid_tot
		hotspot2 floor_size1-floor_size2,step_wid1+landing_gap
		hotspot2 floor_size1,step_wid1+landing_gap
		hotspot2 floor_size1,step_wid1
		hotspot2 0,step_wid1
	endif

	!調整hotspot
	hotspot2 0            ,step_wid1/2,code,floor_size1,1+128 : code=code+1
	hotspot2 floor_size1,step_wid1/2,code,floor_size1,2     : code=code+1
	hotspot2 -1           ,step_wid1/2,code,floor_size1,3     : code=code+1

	hotspot2 floor_size1            ,step_wid_tot/2,code,floor_len,1+128 : code=code+1
	hotspot2 floor_size1+floor_len,step_wid_tot/2,code,floor_len,2     : code=code+1
	hotspot2 floor_size1-1          ,step_wid_tot/2,code,floor_len,3     : code=code+1

	if stair_mode=3 and dis_floor_2D<floor_num then
		hotspot2 floor_size1              ,step_wid_tot-step_wid2/2,code,floor_size2,1+128 : code=code+1
		hotspot2 floor_size1-floor_size2,step_wid_tot-step_wid2/2,code,floor_size2,2     : code=code+1
		hotspot2 floor_size1+1            ,step_wid_tot-step_wid2/2,code,floor_size2,3     : code=code+1
	endif

	!文字
	add2 floor_size1+floor_len/2,step_wid_tot/2
		if turn_left_right then mul2 -1,1
		if turn_up_down then mul2 1,-1
			rot2 -90
			add2 0,font_size/10
				text2 0,0,str(dis_floor_2D,1,0)+"階廊下"
			del 2
		if turn_left_right then del 1
		if turn_up_down then del 1
	del 1
return

