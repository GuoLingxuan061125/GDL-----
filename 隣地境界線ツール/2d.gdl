define style "moji" font_type,font_size*100/glob_scale,5,0
define style "mark" font_type,mark_r*1000/glob_scale,5,0

style "moji"

!==========================================================外周敷地
for i=1 to area_num
    hotspot2 area_pos[i][1],area_pos[i][2]

    hotspot2 0             ,area_pos[i][2],code,area_pos[i][1],1+128:code=code+1
    hotspot2 area_pos[i][1],area_pos[i][2],code,area_pos[i][1],2    :code=code+1
    hotspot2 -1            ,area_pos[i][2],code,area_pos[i][1],3    :code=code+1

    hotspot2 area_pos[i][1],0             ,code,area_pos[i][2],1+128:code=code+1
    hotspot2 area_pos[i][1],area_pos[i][2],code,area_pos[i][2],2    :code=code+1
    hotspot2 area_pos[i][1],-1            ,code,area_pos[i][2],3    :code=code+1

    if i=area_num then
        i2=1
    else
        i2=i+1
    endif

    line2    area_pos[i][1],area_pos[i][2],area_pos[i2][1],area_pos[i2][2]
    hotline2 area_pos[i][1],area_pos[i][2],area_pos[i2][1],area_pos[i2][2]

    if dis_line_num_2d then
        text2 (area_pos[i][1]+area_pos[i2][1])/2,(area_pos[i][2]+area_pos[i2][2])/2,i
    endif
next i

!==========================================================真北マーク
if dis_north_mark then
    add2 (minx+maxx)/2,(miny+maxy)/2
        hotspot2 0              ,north_pos[1][2],code,north_pos[1][1],1+128:code=code+1
        hotspot2 north_pos[1][1],north_pos[1][2],code,north_pos[1][1],2:code=code+1
        hotspot2 -1             ,north_pos[1][2],code,north_pos[1][1],3:code=code+1

        hotspot2 north_pos[1][1],0              ,code,north_pos[1][2],1+128:code=code+1
        hotspot2 north_pos[1][1],north_pos[1][2],code,north_pos[1][2],2:code=code+1
        hotspot2 north_pos[1][1],-1             ,code,north_pos[1][2],3:code=code+1

        hotspot2 mark_r                     ,0                          ,code,north_direction,4+128:code=code+1
        hotspot2 mark_r*cos(north_direction),mark_r*sin(north_direction),code,north_direction,5:code=code+1
        hotspot2 0                          ,0                          ,code,north_direction,6:code=code+1

        circle2 north_pos[1][1],north_pos[1][2],mark_r

        add2 north_pos[1][1],north_pos[1][2]
        rot2 north_direction
            fill mark_fill
        
            poly2_ 5,1+2,
            mark_r,0,0,
            -mark_r*cos(20),mark_r*sin(20),0,
            0,0,900,
            -mark_r*cos(20),-mark_r*sin(20),3000,
            mark_r,0,0

            style "mark"

            add2 2*mark_r,0
            rot2 -90
                text2 0,0,"真北" 
            del 2
        del 2
    del 1
endif

!==========================================================内周敷地
for i=1 to area_num
    hotspot2 area_inside_pos[i][1],area_inside_pos[i][2]

    if i=area_num then
        i2=1
    else
        i2=i+1
    endif

    line_type spotline_linetype

    line2    area_inside_pos[i][1],area_inside_pos[i][2],area_inside_pos[i2][1],area_inside_pos[i2][2]
    hotline2 area_inside_pos[i][1],area_inside_pos[i][2],area_inside_pos[i2][1],area_inside_pos[i2][2]
next i


