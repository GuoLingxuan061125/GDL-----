!==========================================================敷地設定
values "area_num" range[3,]

dim _area_pos[][]

for i=1 to area_num
    _area_pos[i][1]=area_pos[i][1]
    _area_pos[i][2]=area_pos[i][2]
next i

parameters area_pos=_area_pos

!回転角度
dim _line_len[]
dim _line_rtz[]

for i=1 to area_num
    if i=area_num then
        i2=1
    else
        i2=i+1
    endif

    _line_len[i]=sqr((area_pos[i2][1]-area_pos[i][1])^2+(area_pos[i2][2]-area_pos[i][2])^2)

    if area_pos[i2][2]>area_pos[i][2]+eps then
        _line_rtz[i]=acs((area_pos[i2][1]-area_pos[i][1])/_line_len[i])
    endif

    if area_pos[i2][2]>=area_pos[i][2]-eps and area_pos[i2][2]<=area_pos[i][2]+eps then
        if area_pos[i2][1]>=area_pos[i][1] then
            _line_rtz[i]=0
        else
            _line_rtz[i]=180
        endif
    endif

    if area_pos[i2][2]<area_pos[i][2]-eps then
        _line_rtz[i]=360-acs((area_pos[i2][1]-area_pos[i][1])/_line_len[i])
    endif
next i

parameters line_len=_line_len
parameters line_rtz=_line_rtz

!敷地面積
area_outside_area=0

for i=1 to area_num
    if i=area_num then
        i2=1
    else
        i2=i+1
    endif

    area_outside_area=area_outside_area+(_area_pos[i][2]+_area_pos[i2][2])*(_area_pos[i][1]-_area_pos[i2][1])/2
next i

parameters area_outside_area=abs(area_outside_area)
lock "area_outside_area"

!各辺方程式
dim _area_outside_a[]
dim _area_outside_b[]
dim _area_outside_c[]

for i=1 to area_num
    if i=area_num then
        i2=1
    else
        i2=i+1
    endif

    _area_outside_a[i]=_area_pos[i2][2]-_area_pos[i][2]
    _area_outside_b[i]=_area_pos[i][1]-_area_pos[i2][1]
    _area_outside_c[i]=_area_pos[i2][1]*_area_pos[i][2]-_area_pos[i][1]*_area_pos[i2][2]
next i

parameters area_outside_a=_area_outside_a,area_outside_b=_area_outside_b,area_outside_c=_area_outside_c

!辺タイプ
dim _line_class[]

for i=1 to area_num
    _line_class[i]=line_class[i]
next i

parameters line_class=_line_class

values{2} "line_class" 1,"隣地辺",2,"道路辺"

!真北方向の水平距離最低値
values{2} "building_type" 1,"低層住居専用地域",2,"中高層住居専用地域"

if building_type=1 then
    l1=(building_hei-5)/1.25
else
    l1=(building_hei-10)/1.25
endif

parameters l1=l1
lock "l1"

!各辺オフセット値算定
values "offset_min_limit" range[0,]

dim _line_offset[]

for i=1 to area_num
    if line_class[i]=1 then
        _line_offset[i]=max(l1*cos(north_direction-line_rtz[i]+90),offset_min_limit)
    else
        _line_offset[i]=0
    endif
next i

parameters line_offset=_line_offset

!内周各辺方程式
dim _area_inside_a[]
dim _area_inside_b[]
dim _area_inside_c[]

for i=1 to area_num
    if i=area_num then
        i2=1
    else
        i2=i+1
    endif

    _area_inside_a[i]=(area_pos[i2][2]+line_offset[i]*cos(line_rtz[i]))-(area_pos[i][2]+line_offset[i]*cos(line_rtz[i]))
    _area_inside_b[i]=(area_pos[i][1]-line_offset[i]*sin(line_rtz[i]))-(area_pos[i2][1]-line_offset[i]*sin(line_rtz[i]))
    _area_inside_c[i]=(area_pos[i2][1]-line_offset[i]*sin(line_rtz[i]))*(area_pos[i][2]+line_offset[i]*cos(line_rtz[i]))-\
                      (area_pos[i][1]-line_offset[i]*sin(line_rtz[i]))*(area_pos[i2][2]+line_offset[i]*cos(line_rtz[i]))
next i

parameters area_inside_a=_area_inside_a,area_inside_b=_area_inside_b,area_inside_c=_area_inside_c

!内周頂点座標
dim _area_inside_pos[][]

for i=1 to area_num
    if i=1 then
        i1=area_num
    else
        i1=i-1
    endif

    _area_inside_pos[i][1]=(_area_inside_c[i]*_area_inside_b[i1]-_area_inside_c[i1]*_area_inside_b[i])/(_area_inside_a[i1]*_area_inside_b[i]-_area_inside_a[i]*_area_inside_b[i1])
    _area_inside_pos[i][2]=(_area_inside_c[i1]*_area_inside_a[i]-_area_inside_c[i]*_area_inside_a[i1])/(_area_inside_a[i1]*_area_inside_b[i]-_area_inside_a[i]*_area_inside_b[i1])
next i

parameters area_inside_pos=_area_inside_pos

!内周敷地面積
area_inside_area=0

for i=1 to area_num
    if i=area_num then
        i2=1
    else
        i2=i+1
    endif

    area_inside_area=area_inside_area+(_area_inside_pos[i][2]+_area_inside_pos[i2][2])*(_area_inside_pos[i][1]-_area_inside_pos[i2][1])/2
next i

parameters area_inside_area=abs(area_inside_area)
lock "area_inside_area"