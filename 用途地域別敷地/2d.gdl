define style "moji" font_type,font_size*100/glob_scale,5,0

style "moji"

!==========================================================敷地
for i=1 to area_num
    hotspot2 area_pos[i][1],area_pos[i][2]

    hotspot2 0             ,area_pos[i][2],code,area_pos[i][1],1+128:code=code+1
    hotspot2 area_pos[i][1],area_pos[i][2],code,area_pos[i][1],2    :code=code+1
    hotspot2 -1            ,area_pos[i][2],code,area_pos[i][1],3    :code=code+1

    hotspot2 area_pos[i][1],0             ,code,area_pos[i][2],1+128:code=code+1
    hotspot2 area_pos[i][1],area_pos[i][2],code,area_pos[i][2],2    :code=code+1
    hotspot2 area_pos[i][1],-1            ,code,area_pos[i][2],3    :code=code+1

    if i=area_num then
        i2=1
    else
        i2=i+1
    endif

    line2    area_pos[i][1],area_pos[i][2],area_pos[i2][1],area_pos[i2][2]
    hotline2 area_pos[i][1],area_pos[i][2],area_pos[i2][1],area_pos[i2][2]
next i


!==========================================================用途境
set line_type borderline_linetype

dim line_mark[]
line_mark[1]="①"
line_mark[2]="②"
line_mark[3]="③"
line_mark[4]="④"
line_mark[5]="⑤"
line_mark[6]="⑥"
line_mark[7]="⑦"
line_mark[8]="⑧"
line_mark[9]="⑨"
line_mark[10]="⑩"

for i=1 to borderline_num
    hotspot2 borderline_sta[i][1],borderline_sta[i][2]

    hotspot2 0                   ,borderline_sta[i][2],code,borderline_sta[i][1],1+128:code=code+1
    hotspot2 borderline_sta[i][1],borderline_sta[i][2],code,borderline_sta[i][1],2    :code=code+1
    hotspot2 -1                  ,borderline_sta[i][2],code,borderline_sta[i][1],3    :code=code+1

    hotspot2 borderline_sta[i][1],0                   ,code,borderline_sta[i][2],1+128:code=code+1
    hotspot2 borderline_sta[i][1],borderline_sta[i][2],code,borderline_sta[i][2],2    :code=code+1
    hotspot2 borderline_sta[i][1],-1                  ,code,borderline_sta[i][2],3    :code=code+1

    hotspot2 0                   ,borderline_end[i][2],code,borderline_end[i][1],1+128:code=code+1
    hotspot2 borderline_end[i][1],borderline_end[i][2],code,borderline_end[i][1],2    :code=code+1
    hotspot2 -1                  ,borderline_end[i][2],code,borderline_end[i][1],3    :code=code+1

    hotspot2 borderline_end[i][1],0                   ,code,borderline_end[i][2],1+128:code=code+1
    hotspot2 borderline_end[i][1],borderline_end[i][2],code,borderline_end[i][2],2    :code=code+1
    hotspot2 borderline_end[i][1],-1                  ,code,borderline_end[i][2],3    :code=code+1

    if dis_num_2d then
        add2 borderline_sta[i][1],borderline_sta[i][2]
        rot2 borderline_rtz[i]
            text2 -font_size/5,0,line_mark[i]
            text2 0,-font_size/10,"S"
            text2 borderline_len[i],-font_size/10,"E"
        del 2
    endif

    line2    borderline_sta[i][1],borderline_sta[i][2],borderline_end[i][1],borderline_end[i][2]
    hotline2 borderline_sta[i][1],borderline_sta[i][2],borderline_end[i][1],borderline_end[i][2]

    for j=1 to intsec_num[i]
        hotspot2 intsec_pos[i][2*j-1],intsec_pos[i][2*j]
    next j
next i


!==========================================================総頂点
for i=1 to full_num
    if dis_num_2d then
        text2 full_pos[i][1],full_pos[i][2]+font_size/10,i
    endif
next i


!==========================================================用途地域情報表示
if dis_border_2d then
    for i=1 to border_num
        pen border_color[i]

        !輪郭線
        for j=1 to border_vertex_num[i]
            put full_pos[border_vertex_number[i][j]][1],full_pos[border_vertex_number[i][j]][2]
        next j

        poly2 border_vertex_num[i],1+4,get(nsp)

        !番号と面積
        add2 border_center_pos[i][1],border_center_pos[i][2]
            text2 0,0,"エリア"+str(i,1,0)+"  "+str(border_area[i],3,2)+"㎡"
        del 1
    next i
endif






