!========================================================================積算
!スラブ面積(減算前)
slabFullArea=0

for i=1 to slabVertexNum
    if i<slabVertexNum then
        i2=i+1
    else
        i2=1
    endif

    slabFullArea=slabFullArea+(slabVertexPos[i][y]+slabVertexPos[i2][y])*(slabVertexPos[i2][x]-slabVertexPos[i][x])/2
next i

slabFullArea=abs(slabFullArea)
parameters slabFullArea=slabFullArea


!開口面積
slabOpArea=0

for i=1 to opNum
    for j=1 to opVertexNum[i]
        if j<opVertexNum[i] then
            j2=j+1
        else
            j2=1
        endif

        slabOpArea=slabOpArea+(opVertexPos[i][2*j]+opVertexPos[i][2*j2])*(opVertexPos[i][2*j2-1]-opVertexPos[i][2*j-1])/2
    next j
next i

slabOpArea=abs(slabOpArea)
parameters slabOpArea=slabOpArea


!スラブ面積
slabArea=slabFullArea-slabOpArea
parameters slabArea=slabArea


!ボード枚数
boardNum=slabArea/(boardSize1*boardSize2)
parameters boardNum=boardNum

lock "slabFullArea","slabOpArea","slabArea","boardNum"


!天井ふところ高さ(最大値)
slabMaxHei=max(slabHei,(slabHei-switchStep*stepHei))
parameters slabMaxHei=slabMaxHei


!天井ふところ高さ(最小値)
slabMinHei=min(slabHei,(slabHei-switchStep*stepHei))
parameters slabMinHei=slabMinHei


!吊り木高さ(最大値)
hangingMaxHei=slabMaxHei-boardThi-furringHei
parameters hangingMaxHei=hangingMaxHei


!吊り木高さ(最小値)
hangingMinHei=slabMinHei-boardThi-furringHei
parameters hangingMinHei=hangingMinHei

lock "slabMaxHei","slabMinHei","hangingMaxHei","hangingMinHei"