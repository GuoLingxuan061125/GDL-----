!========================================================================屋根基準線
pen baseLinePen

if dis_base then
	for i=1 to 2
		hotspot2 0,base_pos[i][2],code,base_pos[i][1],1+128:code=code+1
		hotspot2 base_pos[i][1],base_pos[i][2],code,base_pos[i][1],2:code=code+1
		hotspot2 -1,base_pos[i][2],code,base_pos[i][1],3:code=code+1
	
		hotspot2 base_pos[i][1],0,code,base_pos[i][2],1+128:code=code+1
		hotspot2 base_pos[i][1],base_pos[i][2],code,base_pos[i][2],2:code=code+1
		hotspot2 base_pos[i][1],-1,code,base_pos[i][2],3:code=code+1
	next i

	line2 base_pos[1][1],base_pos[1][2],base_pos[2][1],base_pos[2][2]

	add2 (base_pos[1][1]+base_pos[2][1])/2,(base_pos[1][2]+base_pos[2][2])/2
	rot2 base_rtz
		line2 0,0,0,-0.2*base_dir
		hotspot2 0,0,code,base_dir,1+128:code=code+1
		hotspot2 0,-0.2*base_dir,code,base_dir,2:code=code+1
		hotspot2 0,1,code,base_dir,3:code=code+1
	del 2
endif




!========================================================================スラブ
pen normalPen

for i=1 to slabVertexNum
    if disHotspot=1 then
        hotspot2 0                   ,slabVertexPos[i][y] ,code ,slabVertexPos[i][x] ,1+128 :code=code+1
        hotspot2 slabVertexPos[i][x] ,slabVertexPos[i][y] ,code ,slabVertexPos[i][x] ,2     :code=code+1
        hotspot2 -1                  ,slabVertexPos[i][y] ,code ,slabVertexPos[i][x] ,3     :code=code+1

        hotspot2 slabVertexPos[i][x] ,0                   ,code ,slabVertexPos[i][y] ,1+128 :code=code+1
        hotspot2 slabVertexPos[i][x] ,slabVertexPos[i][y] ,code ,slabVertexPos[i][y] ,2     :code=code+1
        hotspot2 slabVertexPos[i][x] ,-1                  ,code ,slabVertexPos[i][y] ,3     :code=code+1
    else
        hotspot2 slabVertexPos[i][x] ,slabVertexPos[i][y]
    endif

    if i<slabVertexNum then
        i2=i+1
    else
        i2=1
    endif

    line2 slabVertexPos[i][x] ,slabVertexPos[i][y] ,slabVertexPos[i2][x] ,slabVertexPos[i2][y]
next i    


!========================================================================開口
for i=1 to opNum
    for j=1 to opVertexNum[i]
        if disHotspot=2 then
            hotspot2 0                     ,opVertexPos[i][2*j] ,code ,opVertexPos[i][2*j-1] ,1+128 :code=code+1
            hotspot2 opVertexPos[i][2*j-1] ,opVertexPos[i][2*j] ,code ,opVertexPos[i][2*j-1] ,2     :code=code+1
            hotspot2 -1                    ,opVertexPos[i][2*j] ,code ,opVertexPos[i][2*j-1] ,3     :code=code+1

            hotspot2 opVertexPos[i][2*j-1] ,0                   ,code ,opVertexPos[i][2*j] ,1+128 :code=code+1
            hotspot2 opVertexPos[i][2*j-1] ,opVertexPos[i][2*j] ,code ,opVertexPos[i][2*j] ,2     :code=code+1
            hotspot2 opVertexPos[i][2*j-1] ,-1                  ,code ,opVertexPos[i][2*j] ,3     :code=code+1
        else
            hotspot2 opVertexPos[i][2*j-1] ,opVertexPos[i][2*j]
        endif

        if j<opVertexNum[i] then
            j2=j+1
        else
            j2=1
        endif

        line2 opVertexPos[i][2*j-1] ,opVertexPos[i][2*j] ,opVertexPos[i][2*j2-1] ,opVertexPos[i][2*j2]
    next j
next i


!========================================================================段差
if switchStep then
    for i=1 to stepVertexNum
        if disHotspot=4 then
            hotspot2 0                   ,stepVertexPos[i][y] ,code ,stepVertexPos[i][x] ,1+128 :code=code+1
            hotspot2 stepVertexPos[i][x] ,stepVertexPos[i][y] ,code ,stepVertexPos[i][x] ,2     :code=code+1
            hotspot2 -1                  ,stepVertexPos[i][y] ,code ,stepVertexPos[i][x] ,3     :code=code+1

            hotspot2 stepVertexPos[i][x] ,0                   ,code ,stepVertexPos[i][y] ,1+128 :code=code+1
            hotspot2 stepVertexPos[i][x] ,stepVertexPos[i][y] ,code ,stepVertexPos[i][y] ,2     :code=code+1
            hotspot2 stepVertexPos[i][x] ,-1                  ,code ,stepVertexPos[i][y] ,3     :code=code+1
        else
            hotspot2 stepVertexPos[i][x] ,stepVertexPos[i][y]
        endif

        if i<stepVertexNum then
            i2=i+1
        else
            i2=1
        endif

        line2 stepVertexPos[i][x] ,stepVertexPos[i][y] ,stepVertexPos[i2][x] ,stepVertexPos[i2][y]
    next i
endif


!========================================================================割り付け芯
if disCenter then
    !ホットスポット
    if disHotspot=3 then
        hotspot2 0       ,centerY ,code ,centerX ,1+128 :code=code+1
        hotspot2 centerX ,centerY ,code ,centerX ,2     :code=code+1
        hotspot2 -1      ,centerY ,code ,centerX ,3     :code=code+1

        hotspot2 centerX ,0       ,code ,centerY ,1+128 :code=code+1
        hotspot2 centerX ,centerY ,code ,centerY ,2     :code=code+1
        hotspot2 centerX ,-1      ,code ,centerY ,3     :code=code+1
  
        hotspot2 centerX+0.2                ,centerY                    ,code ,centerRtz ,4+128 :code=code+1
        hotspot2 centerX+0.2*cos(centerRtz) ,centerY+0.2*sin(centerRtz) ,code ,centerRtz ,5     :code=code+1
        hotspot2 centerX                    ,centerY                    ,code ,centerRtz ,6     :code=code+1
    else   
        hotspot2 centerX ,centerY
    endif

    !矢印
    poly2_ 2 ,1 ,
    centerX ,centerY ,900 ,
    0.1 ,360 ,4001

    add2 centerX ,centerY
    rot2 centerRtz
        line2 -0.2 ,0    ,0.2       ,0
        line2 0    ,-0.2 ,0         ,0.2
        line2 -0.2 ,0    ,-0.2+0.05 ,0.05
        line2 -0.2 ,0    ,-0.2+0.05 ,-0.05
        line2 0.2  ,0    ,0.2-0.05  ,0.05
        line2 0.2  ,0    ,0.2-0.05  ,-0.05
        line2 0    ,-0.2 ,0.05      ,-0.2+0.05
        line2 0    ,-0.2 ,-0.05     ,-0.2+0.05
        line2 0    ,0.2  ,0.05      ,0.2-0.05
        line2 0    ,0.2  ,-0.05     ,0.2-0.05
    del 2
endif


!========================================================================分割線
project2{3} 3,270,1,1+2+4+8+16+32,parameters disLine3D1=disLine2D,disLine3D2=0,disBoard3D=0,disCenter=0

