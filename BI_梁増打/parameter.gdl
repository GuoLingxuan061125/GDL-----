!=====================================================梁設定
values "beam_left_wid" range[0,]
values "beam_right_wid" range[0,]

if switch_wid_equal then
    parameters beam_left_wid=beam_right_wid
    hideparameter "beam_left_wid"
endif

beam_len=sqr((beam_pos[2][1]-beam_pos[1][1])^2+(beam_pos[2][2]-beam_pos[1][2])^2)

parameters beam_len=beam_len
lock "beam_len"

beam_rtz=0

if beam_len>0 then
    if beam_pos[2][2]>beam_pos[1][2]+eps then
        beam_rtz=acs((beam_pos[2][1]-beam_pos[1][1])/beam_len)
    endif

    if beam_pos[2][2]>=beam_pos[1][2]-eps and beam_pos[2][2]<=beam_pos[1][2]+eps then
        if beam_pos[2][1]>=beam_pos[1][1] then
            beam_rtz=0
        else
            beam_rtz=180
        endif
    endif

    if beam_pos[2][2]<beam_pos[1][2]-eps then
        beam_rtz=360-acs((beam_pos[2][1]-beam_pos[1][1])/beam_len)
    endif
endif

parameters beam_rtz=beam_rtz



values "beam_top_point_num" range[0,]

dim _beam_top_point_pos[][]

if beam_top_point_num>0 then
    for i=1 to beam_top_point_num
        if beam_top_point_pos[i][1]<0 then
            _beam_top_point_pos[i][1]=0
        else
            if beam_top_point_pos[i][1]>beam_len then
                _beam_top_point_pos[i][1]=beam_len
            else
                _beam_top_point_pos[i][1]=beam_top_point_pos[i][1]
            endif
        endif

        if beam_top_point_pos[i][2]<0 then
            _beam_top_point_pos[i][2]=0
        else
            _beam_top_point_pos[i][2]=beam_top_point_pos[i][2]
        endif
    next i
else
    _beam_top_point_pos[1][1]=0
endif

parameters beam_top_point_pos=_beam_top_point_pos


!=====================================================体積算定
!右側表面積
dim beam_right_pos[][]

put beam_len,0        -beam_right_wid*top_rat 
put beam_len,-beam_hei-beam_right_wid*down_rat
put 0       ,-beam_hei-beam_right_wid*down_rat
put 0       ,0        -beam_right_wid*top_rat 

for i=1 to beam_top_point_num
    put beam_top_point_pos[i][1],beam_top_point_pos[i][2]-beam_right_wid*top_rat
next i

for i=1 to 4+beam_top_point_num
    beam_right_pos[i][1]=get(1)
    beam_right_pos[i][2]=get(1)
next i

right_area=0

for i=1 to 4+beam_top_point_num
    if i<4+beam_top_point_num then
        i2=i+1
    else
        i2=1
    endif

    right_area=right_area+(beam_right_pos[i][2]+beam_right_pos[i2][2])*(beam_right_pos[i2][1]-beam_right_pos[i][1])/2
next i

right_area=abs(right_area)


!左側表面積
dim beam_left_pos[][]

put beam_len,0        +beam_left_wid*top_rat
put beam_len,-beam_hei+beam_left_wid*down_rat
put 0       ,-beam_hei+beam_left_wid*down_rat
put 0       ,0        +beam_left_wid*top_rat

for i=1 to beam_top_point_num
    put beam_top_point_pos[i][1],beam_top_point_pos[i][2]+beam_left_wid*top_rat
next i

for i=1 to 4+beam_top_point_num
    beam_left_pos[i][1]=get(1)
    beam_left_pos[i][2]=get(1)
next i

left_area=0

for i=1 to 4+beam_top_point_num
    if i<4+beam_top_point_num then
        i2=i+1
    else
        i2=1
    endif

    left_area=left_area+(beam_left_pos[i][2]+beam_left_pos[i2][2])*(beam_left_pos[i2][1]-beam_left_pos[i][1])/2
next i

left_area=abs(left_area)


!体積
beam_vol=(right_area+left_area)/2*(beam_right_wid+beam_left_wid)
parameters beam_vol=beam_vol
lock "beam_vol"